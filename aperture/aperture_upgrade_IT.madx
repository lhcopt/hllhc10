! Author: J. Miles based on preliminary data from N. Kos, P. Fessia, R. Jones - February 2009
! THE FOLLOWING CODE IS USED TO GENERATE THE BEAM SCREEN APERTURE
! MODEL FOR THE PHASE 1 UPGRADE. 
!
! Three different BS cross sections are available, namely:
!
! OCTAGON, CIRCLE, RECTELL
!
! The optimised octagon can be constructed using only 2 parameters, thanks
! to its symmetry. The first parameter is the BS outer radius between corners
! - called R1 by N. Kos - at 293K (RT). The second parameter is the angle subtended
! at the centre of the BS by the flat (without cooling tubes) between corners. 
! This angle is typically 20 degrees and the flats are "short". Additional
! radii are called R2 and R3 - the outer radius between flats with cooling tubes
! and the outer radius between flats without colling tubes, respectively - but
! they are not required to construct the octagon.
!
! The BS outer radius R1 is derived from the nominal cold bore inner diameter
! by applying the appropriate manufacturing tolerances. For details, see EDMS
! 350449 (N. Kos). The formula for R1 is given below.
!
! The aperture dimensions are derived from the optimised octagon and they
! can be applied to all three BS cross-setions. 
!
! The aperture in the octagon at 5K is calculated by applying manufacturing tolerances
! to R1 and R2 according to ISO standards for seamless tubes and pipes of a given
! wall thickness (base material plus coating):
!   outside diameter :   D4  (+- 0.5%)
!   wall thickness   :   T3  (+- 10%)
! For details, see EDMS 350449 (N.Kos). The resulting dimensions, called A1 and 
! A2 respectively, are sufficient to specify the aperture completely. (In fact,
! only A1 together with the angle between (short) flats are required).
!
! The circular BS corresponds to the largest circle inscribed in the 
! octagon: that is to say, a circle with outer radius R2 which is
! tangential to the cooling tube flats. Therfore, the radius of the circular 
! aperture is A2. 
!
! The cross-section of the BS installed in the current machine (2008) is the 
! superposition of a rectangle and a circle to obtain a "truncated circle". 
! This is implemented in MAD by a RECTELLIPSE, which requires only 2 different
! parameters A1 and A2, that are applied according the desired BS orientation. 
!
! The triplet quadrupole at Q1 contains a thick absorber which is included in
! the calculation of A1 for Q1 only.
!
! The radial BS size (corner) at RT for a given cold bore diameter is calculated
! as follows (shortened variable names, D means "delta"):
!
! R1 = ID/2 - DID/2 - g - d - Dd - Db 
!
! The half-aperture (corner) at 5K for a given CB diameter is calculated
! as follows (shortened variable names, D means "delta"):
!
! A1 = 0.99727(ID/2 - 3DID/2 - 2g - d - 3Dd - 4Db - w - Dw - bssl)
!
! where :
! R1     = radial BS size 
! A1     = vertical BS half-aperture
! P506 thermal contraction factor (293K -> 5K) = 0.99727
! CB_ID  = nominal CB inner diameter
! DCB_ID = tolerance on CB inner diameter
! MIN_g  = minimum radial gap
! BS_d   = nominal BS support thickness
! DBS_d  = tolerance on BS support thickness
! DBS_b  = tolerance on BS outer half-dimension
! BS_w   = nominal BS wall thickness	
! DBS_w  = tolerance on BS wall thickness
! BS_s   = BS straightness error  (m/m)
! BS_l   = longitudinal distance between rings (mm)
! BS_A   = absorber thickness (mm)

! 20/01/2012: scaling defined externally
! 19/04/2012: new round implemented

CB_ID  := 110.6;      ! this corresponds to a 120mm coil diameter
DCB_ID := 1.2;
MIN_g  := 0.1;
BS_d   := 0.4;
DBS_d  := 0.03;
DBS_b  := 0.25;
BS_w   := 1.575;
DBS_w  := 0.1;
BS_s   := 0.001;
BS_l   := 400.0;
BS_A   := 6.0;        !was 9 mm and then 9.8 mm in previous version
!
! ... everything is derived from the  optimised OCTAGON
!ap_mqx=120;
scaleap=ap_mqx/120.;      ! rescaling the IT to actual aperture
Q2Q3_R1 := CB_ID/2.-DCB_ID/2.-MIN_g-BS_d-DBS_d-DBS_b;
Q2Q3_A1 := scaleap*(0.99727*(CB_ID/2.-3.*DCB_ID/2.-2.*MIN_g-BS_d-3.*DBS_d-4.*DBS_b-BS_w-DBS_w-BS_s*BS_l));
Q1_A1   := (Q2Q3_A1-0.99727*BS_A);
!
phi_deg := 21.365;      ! angle subtended by flats without cooling tubes and fixed to leave 5.2 mm
                        ! for the cooling tubes 
                        ! was 20 degrees in SLHCV1.0, then 18.529 in SLHCV2.0
phi_rad := phi_deg*TWOPI/360.;
! ... symmetry allows us to work only with parallels to the corner (short) flats
Q2Q3_xx1 := 1000.*Q2Q3_A1;
Q2Q3_yy1 := 0.0;
Q2Q3_xx2 := 1000.*(Q2Q3_A1*cos(phi_rad));
Q2Q3_yy2 := 1000.*(Q2Q3_A1*sin(phi_rad));
! ... compute slope and intercept of flats; this is used to locate the
! ... absorber in Q1
! ... parallel to the BS short flat
Qm1 := (Q2Q3_yy2 - Q2Q3_yy1)/(Q2Q3_xx2 - Q2Q3_xx1);
Qc1 := (Q2Q3_yy1 - Qm1*Q2Q3_xx1)/1000.;
! ... parallel to the BS long flat (with cooling tube)
Qm2 := -1.;
Qc2 := (Q2Q3_yy2 - Qm2*Q2Q3_xx2)/1000.;
! ... A2 is computed from the intersection of a 45 deg line and 
! ... a normal parallel to the BS long flat (with a cooling tube)
Q2Q3_A2 := sqrt(Qc2*Qc2/2.);
Q1_A2   := Q2Q3_A2-0.99727*BS_A;
! ... locate a parallel to the face of the absorber
Q1_xx1 := 1000.*Q1_A1;
Q1_yy1 := 0.0;
Q1_xx2 := 1000.*Q1_A1;
Q1_yy2 := 1000.*(Qm2*Q1_A1 + Qc2);
!
! *** macro definitions ***
Q2Q3_A(xx1,yy1,xx2,yy2): macro = {
print,text="0.0xx1  0.0yy1";
print,text="0.0xx2  0.0yy2";
print,text="0.0yy2  0.0xx2";
print,text="0.0yy1  0.0xx1";
print,text="-0.0yy2  0.0xx2";
print,text="-0.0xx2  0.0yy2";
print,text="-0.0xx1  0.0yy1";
print,text="-0.0xx2 -0.0yy2";
print,text="-0.0yy2 -0.0xx2";
print,text="0.0yy1  -0.0xx1";
print,text="0.0yy2  -0.0xx2";
print,text="0.0xx2  -0.0yy2";
print,text="0.0xx1  0.0yy1";
};
Q1_A(xx1,yy1,xx2,yy2): macro = {
print,text="0.0xx1  0.0yy1";
print,text="0.0xx2  0.0yy2";
print,text="0.0yy2  0.0xx2";
print,text="-0.0yy2  0.0xx2";
print,text="-0.0xx2  0.0yy2";
print,text="-0.0xx2 -0.0yy2";
print,text="-0.0yy2 -0.0xx2";
print,text="0.0yy2  -0.0xx2";
print,text="0.0xx2  -0.0yy2";
print,text="0.0xx1  0.0yy1";
};
Q1_B(xx1,yy1,xx2,yy2): macro = {
print,text="0.0xx1   0.0yy1";
print,text="0.0xx2   0.0yy2";
print,text="-0.0xx2  0.0yy2";
print,text="-0.0xx1  0.0yy1";
print,text="-0.0xx1 -0.0yy1";
print,text="-0.0xx2 -0.0yy2";
print,text="0.0xx2  -0.0yy2";
print,text="0.0xx1  -0.0yy1";
print,text="0.0xx1   0.0yy1";
};

! *** end of macro definitions ***
!
OCTAGON = 1;
CIRCLE  = 2;
RECTELL = 3;
REGOCTA = 4;
REGOCTATOLL = 5;
if (bs_type==0){  bs_type=1;};

if (bs_type == OCTAGON)
{
 system, "rm  -f Q1_APERTURE";
 system, "rm  -f Q2Q3_APERTURE";

 assign, echo="Q1_APERTURE";
 exec, Q1_A($Q1_xx1, $Q1_yy1, $Q1_xx2, $Q1_yy2);
 assign, echo=terminal;

 assign, echo="Q2Q3_APERTURE";
 exec, Q2Q3_A($Q2Q3_xx1, $Q2Q3_yy1, $Q2Q3_xx2, $Q2Q3_yy2);
 assign, echo=terminal;

 call, file="slhc/aperture/aperture_upgrade_octagon.madx";

};

if (bs_type == CIRCLE)
{
Q2Q3_R1 := CB_ID/2.*scaleap-DCB_ID/2.-BS_A/2;
r_Q2Q3:= Q2Q3_R1/1000.;
g_Q2Q3:= Q2Q3_R1/1000.;
r_Q1  := Q2Q3_R1/1000.;
g_Q1  := Q2Q3_R1/1000.;
value,r_Q1*2,g_Q1,r_Q2Q3,g_Q2Q3;

 call, file="slhc/aperture/aperture_upgrade_rectellipse.madx";

};

if (bs_type == RECTELL)
{
r_Q2Q3:= Q2Q3_A1/1000.;
g_Q2Q3:= Q2Q3_A2/1000.;
r_Q1  := Q1_A1/1000.  ;     ! absorber is 9mm at RT
g_Q1  := Q1_A2/1000.  ;

 call, file="slhc/aperture/aperture_upgrade_rectellipse.madx";

};


if (bs_type == REGOCTA)
{
 value,ap_mqx;
 system, "rm Q1_APERTURE";
 system, "rm Q2Q3_APERTURE";
ap_mqx=150;
EL_T =1.5; ! helium thichness
CB_T =5; ! cold bore thickness !it was (120-110.6)/2*1.25=5.875
CB_BS=1.5; ! cold bore to beam screen
BS_T =2.0; ! beam screen thickness
W_T  =6.0; ! tungsten thichness
!K_T  =0.99727; ! P506 thermal contraction factor (293K -> 5K) = 0.99727
angle1=22.5;angle2=67.5;
x_Q1=(ap_mqx/2-EL_T-CB_T-CB_BS-BS_T-W_T);
r_Q1=x_Q1/cos(angle1/180*pi);
 value,x_Q1*2,r_Q1*2; ! mid plane distance and largest radius in the vacuum
 xx1=r_Q1*cos(angle1/180*pi)*1000;
 yy1=r_Q1*sin(angle1/180*pi)*1000;
 xx2=r_Q1*cos(angle2/180*pi)*1000;
 yy2=r_Q1*sin(angle2/180*pi)*1000;
 value,xx1,yy1,xx2,yy2;

 assign, echo="Q2Q3_APERTURE";
 exec,Q1_B($xx1,$yy1,$xx2,$yy2);
 assign, echo=terminal;

 W_T  =16.0; ! tungsten thichness
 angle1=22.5;angle2=67.5;
 x_Q1=(ap_mqx/2-EL_T-CB_T-CB_BS-BS_T-W_T);
 r_Q1=x_Q1/cos(angle1/180*pi);
! value,x_Q1*2,r_Q1*2; ! mid plane distance and largest radius in the vacuum
 xx1=r_Q1*cos(angle1/180*pi)*1000;
 yy1=r_Q1*sin(angle1/180*pi)*1000;
 xx2=r_Q1*cos(angle2/180*pi)*1000;
 yy2=r_Q1*sin(angle2/180*pi)*1000;
! value,xx1,yy1,xx2,yy2;

 assign, echo="Q1_APERTURE";
 exec,Q1_B($xx1,$yy1,$xx2,$yy2);
 assign, echo=terminal;

 call, file="slhc/aperture/aperture_upgrade_octagon.madx";
};


if (bs_type == REGOCTATOLL)
{
 system, "rm  -f Q1_APERTURE";
 system, "rm  -f Q2Q3_APERTURE";

 assign, echo="Q1_APERTURE";
 exec, Q1_A($Q1_xx1, $Q1_yy1, $Q1_xx2, $Q1_yy2);
 assign, echo=terminal;

 assign, echo="Q2Q3_APERTURE";
 exec, Q1_A($Q1_xx1, $Q1_yy1, $Q1_xx2, $Q1_yy2);
 assign, echo=terminal;

 call, file="slhc/aperture/aperture_upgrade_octagon.madx";

};

